function affordable_real(
    irreducible_characters,
    multiplicities,
)
    irr_real = similar(irreducible_characters, 0)
    mls_real = similar(multiplicities, 0)
    for (i, œá) in pairs(irreducible_characters)
        Œπ = Characters.frobenius_schur(œá)
        if Œπ == 1 # real
            @debug "real" œá
            push!(irr_real, œá)
            push!(mls_real, multiplicities[i])
        elseif Œπ == -1 # quaternionic
            @debug "quaternionic" œá
            @assert iseven(multiplicities[i]) "Multiplicities of quaternionic character must be even."
            push!(irr_real, 2 * œá)
            push!(mls_real, div(multiplicities[i], 2))
        else # complex
            cœá = conj(œá)
            k = findfirst(==(cœá), irreducible_characters)
            @assert k !== nothing "Conjugate character not found."
            @debug "complex" œá conj(œá) = irreducible_characters[k]
            if k > i # we haven't already observed a conjugate
                @assert multiplicities[i] == multiplicities[k] "Multiplicities of complex conjugates must be equal."
                push!(irr_real, œá + cœá)
                push!(mls_real, multiplicities[i])
            end
        end
    end

    return irr_real, mls_real
end

function symmetry_adapted_basis(
    G::PG.AbstractPermutationGroup,
    S::Type = Rational{Int};
    semisimple::Bool = false,
)
    tbl = CharacterTable(S, G)
    return symmetry_adapted_basis(eltype(tbl), tbl; semisimple = semisimple)
end

"""
    symmetry_adapted_basis([T::Type,] G::AbstractPermutationGroup[, S=Rational{Int};
        semisimple=false])
Compute a decomposition of `V=ùïÇ‚Åø` (with `n = degree(G)`) into
subspaces invariant under the natural permutation action of `G`.

Consider the decomposition of `V` into irreducible (simple) subspaces
    `V ‚âÖ m‚ÇÅV‚ÇÅ ‚äï ‚ãØ ‚äï m·µ£V·µ£`
and write `W·µ¢ = m·µ¢V·µ¢` (the `m·µ¢`-fold direct sum of `V·µ¢`). The decomposition is
returned as a vector of `DirectSummand{T}`s (blocks) corresponding
to the distinct irreducible characters of `G` (i.e. types of action, here from `1`
to `r`). Each block contains a basis for a `G`-invariant subspace in `V` (`V·µ¢` or `W·µ¢`). The blocks are guaranteed to be orthogonal to **each other**, however vectors within a single block may *not* be orthogonal.

If `T<:LinearAlgebra.BlasFloat` BLAS routines will be used to orthogonalize
vectors within each block.

!!! note:
Each returned block is invariant (as a subspace) under the action of `G`, which
means that the action may still e.g. permute (row) vectors, but only *within* each block.

Arguments:
* `T` controls the type of coefficients of the returned basis. Unless specified,
the coefficients will be computed exactly in the field of cyclotomic numbers.
If you know that the group has rational characters only (which happens e.g. for
the full symmetric groups) You may specify `Rational{Int}` here. For a group
with complex characters specifying `T<:Real` will result in the computation of the realified basis.
* `S` controls the types of `Cyclotomic`s used in the computation of
character table. Exact type are preferred. For larger groups `G` (or if
overflow occurs during the computation of characters) specifying
`Rational{BigInt}` might be necessary.
* `semisimple::Bool` controls the nature of the the returned basis.
  - `semisimple=true`: the returned basis consists of orthogonal blocks
  (`DirectSummand`s) which define an isomorphism
  `V ‚âÖ W‚Çñ ‚äï ‚ãØ ‚äï W‚Çñ`.
  associated to isotypical components `W‚Çñ`, which are (in general) semi-simple.
  I.e. each direct summand `ds` may further decopose into `G`-invariant
  subspaces `W‚Çñ ‚âÖ m‚ÇñV‚Çñ`, all of the same _type_. Multiplicity `m‚Çñ` can be
  obtained by calling `multiplicity(@ref)`.
  - `semisimple=false`: (the default) In addition to finding blocks `W‚Çñ`, an
  effort to find _minimal projection system_ is made, i.e all, some (or none!)
  of the returned blocks corresponds to a **projection** `V ‚Üí W‚Çñ ‚âÖ m‚ÇñV‚Çñ ‚Üí V‚Çñ` for a single irreducible subspace `V‚Çñ`. This means that some blocks can not be further decomposed into nontrivial `G`-invariant subspaces.
"""
function symmetry_adapted_basis(
    T::Type,
    G::PG.AbstractPermutationGroup,
    S::Type = Rational{Int};
    semisimple::Bool = false,
)
    return symmetry_adapted_basis(
        T,
        CharacterTable(S, G);
        semisimple = semisimple,
    )
end

function symmetry_adapted_basis(
    T::Type,
    tbl::CharacterTable;
    semisimple::Bool = false,
)
    irr, multips = _constituents_decomposition(
        action_character(T, conjugacy_classes(tbl), tbl),
        tbl,
    )

    if T <: Real
        irr, multips = affordable_real(irr, multips)
    end

    if semisimple || all(isone ‚àò degree, irr)
        return _symmetry_adapted_basis(T, irr, multips)
    else
        RG = _group_algebra(parent(tbl))
        return _symmetry_adapted_basis(T, irr, multips, RG)
    end
end

"""
    symmetry_adapted_basis([T::Type,] G::Group, action, basis[, S=Rational{Int}];
        semisimple=false)
Compute a decomposition of `V=‚ü®basis‚ü©` into subspaces invariant under the
given action of `G`.

It is assumed that `G` acts on a subset of basis and the action needs to be
extended to the whole `basis`. If `G` is a permutation group already acting on
the whole `basis`, a call to `symmetry_adapted_basis(G)` is preferred.

* For inducing the action `basis` needs to be indexable and iterable
(e.g. in the form of an `AbstractVector`).

!!! note !!!
    A [GroupActionError](@ref) is an indicator that the group action defined by
    `(G, action, basis)` might be incorrect.
"""
function symmetry_adapted_basis(
    G::Group,
    action::Action,
    basis,
    S::Type = Rational{Int};
    semisimple = false,
)
    tbl = CharacterTable(S, G)
    ehom = SchreierExtensionHomomorphism(
        parent(tbl),
        action,
        basis;
        memoize = true,
    )
    check_group_action(G, ehom; full_check = false)
    return symmetry_adapted_basis(
        eltype(tbl),
        tbl,
        ehom;
        semisimple = semisimple,
    )
end

function symmetry_adapted_basis(
    T::Type,
    G::Group,
    action::Action,
    basis,
    S::Type = Rational{Int};
    semisimple = false,
)
    tbl = CharacterTable(S, G)
    ehom = SchreierExtensionHomomorphism(
        parent(tbl),
        action,
        basis;
        memoize = true,
    )
    check_group_action(G, ehom; full_check = false)
    return symmetry_adapted_basis(T, tbl, ehom; semisimple = semisimple)
end

function symmetry_adapted_basis(
    T::Type,
    tbl::CharacterTable,
    ehom::InducedActionHomomorphism;
    semisimple = false,
)
    œà = action_character(ehom, tbl)

    irr, multips = _constituents_decomposition(œà, tbl)
    if T <: Real
        irr, multips = affordable_real(irr, multips)
        @debug "Decomposition into real character spaces:
        degrees:        $(join([lpad(d, 6) for d in degree.(irr)], ""))
        multiplicities: $(join([lpad(m, 6) for m in multips], ""))"
    end

    if semisimple || all(isone ‚àò degree, irr)
        return _symmetry_adapted_basis(T, irr, multips, ehom)
    else
        RG = _group_algebra(parent(tbl))
        return _symmetry_adapted_basis(T, irr, multips, RG, ehom)
    end
end

function _constituents_decomposition(œà::Character, tbl::CharacterTable)
    irr = irreducible_characters(tbl)
    degrees = degree.(irr)
    multips = multiplicities(œà)

    @debug "Decomposition into character spaces:
    degrees:        $(join([lpad(d, 6) for d in degrees], ""))
    multiplicities: $(join([lpad(m, 6) for m in multips], ""))"

    @assert dot(multips, degrees) == degree(œà)
    "Something went wrong: characters do not constitute a complete basis for action:
    $(dot(multips, degrees)) ‚â† $(degree(œà))"

    present_irreps = [i for (i, m) in pairs(multips) if m ‚â† 0]
    return irr[present_irreps], multips[present_irreps]
end

function _symmetry_adapted_basis(
    T::Type,
    irr::AbstractVector{<:Character},
    multiplicities::AbstractVector{<:Integer},
    hom = nothing,
)
    res = map(zip(irr, multiplicities)) do (œá, m)
        Threads.@spawn begin
            œáT = eltype(œá) == T ? œá : Character{T}(œá)
            # here we use algebra to compute the dimension of image;
            # direct summand is simple only if rk == m, i.e. deg == 1
            rk = m * degree(œá)
            image =
                isnothing(hom) ? image_basis(œáT, rk) : image_basis(hom, œáT, rk)
            DirectSummand(image, m, œá)
        end
    end
    return fetch.(res)
end

function _symmetry_adapted_basis(
    T::Type,
    irr::AbstractVector{<:Character},
    multips::AbstractVector{<:Integer},
    RG::StarAlgebra{<:Group},
    hom = nothing,
)
    mps, ranks = minimal_projection_system(irr, RG)
    @debug "ranks of projections obtained by mps:" degrees
    res = map(zip(mps, irr, multips, ranks)) do (¬µ, œá, m, r)
        Threads.@spawn begin
            ¬µT = eltype(¬µ) == T ? ¬µ : AlgebraElement{T}(¬µ)
            # here we use algebra to compute the dimension of image;
            # direct summand is simple only if rk == m, i.e. r == 1
            rk = m * r
            image =
                isnothing(hom) ? image_basis(¬µT, rk) : image_basis(hom, ¬µT, rk)
            return DirectSummand(image, m, œá)
        end
    end
    direct_summands = fetch.(res)

    for (œá, ds) in zip(irr, direct_summands)
        if issimple(ds) &&
           (d = size(ds, 1)) !=
           (e = multiplicity(ds) * sum(multiplicities(œá) .> 0))
            throw(
                "The dimension of the projection doesn't match with simple summand multiplicity: $d ‚â† $e",
            )
        end
    end

    return direct_summands
end
